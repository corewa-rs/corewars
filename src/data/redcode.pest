WHITESPACE = _{ " " | "\t" }

COMMENT = @{ ";" ~ (!NEWLINE ~ ANY)* ~ &NEWLINE }

RedcodeFile = { SOI ~ (Line ~ NEWLINE)+ ~ Line? ~ EOI }

Line = { LabelDeclaration* ~ Instruction? }

Instruction = _{ Operation ~ Field ~ ("," ~ Field)? }

LabelDeclaration = _{ !Operation ~ Label ~ (":")? }

Label = @{ Alpha ~ Alphanumeral* }

Operation = ${ Opcode ~ ("." ~ Modifier)? }

Field = !{ AddressMode? ~ Expr }

Opcode = {
    ^"DAT" | ^"MOV" | ^"ADD" | ^"SUB" | ^"MUL" | ^"DIV" | ^"MOD" |
    ^"JMP" | ^"JMZ" | ^"JMN" | ^"DJN" | ^"CMP" | ^"SEQ" | ^"SNE" |
    ^"SLT" | ^"SPL" | ^"NOP" | ^"ORG" | ^"EQU" | ^"END"
}

Modifier = { ^"AB" | ^"BA" | ^"A" | ^"B" | ^"F" | ^"X" | ^"I" }

AddressMode = { "#" | "$" | "*" | "@" | "{" | "<" | "}" | ">" }

Expr = { Number | Label }

Number = @{ ("-" | "+")? ~ ASCII_DIGIT+ }

Alpha = _{ ASCII_ALPHA | "_" }

Alphanumeral = _{ ASCII_ALPHANUMERIC | "_" }
