WHITESPACE = _{ " " | "\t" }

COMMENT = @{ ";" ~ (!NEWLINE ~ ANY)* ~ &NEWLINE }

AssemblyFile = { SOI ~ Line+ ~ EOI }

Line = { Instruction? ~ NEWLINE }

Instruction = _{ LabelList ~ Operation ~ Field ~ ("," ~ Field)? }

LabelList = _{ (!Operation ~ Label ~ NEWLINE*)* }

Label = @{ Alpha ~ Alphanumeral* }

Operation = ${ Opcode ~ ("." ~ Modifier)? }

Field = !{ AddressMode? ~ Expr }

Opcode = {
    ^"DAT" | ^"MOV" | ^"ADD" | ^"SUB" | ^"MUL" | ^"DIV" | ^"MOD" |
    ^"JMP" | ^"JMZ" | ^"JMN" | ^"DJN" | ^"CMP" | ^"SEQ" | ^"SNE" |
    ^"SLT" | ^"SPL" | ^"NOP" | ^"ORG" | ^"EQU" | ^"END"
}

Modifier = { ^"AB" | ^"BA" | ^"A" | ^"B" | ^"F" | ^"X" | ^"I" }

AddressMode = { "#" | "$" | "*" | "@" | "{" | "<" | "}" | ">" }

Expr = { Number }

Number = @{ ASCII_DIGIT+ }

Alpha = _{ ASCII_ALPHA | "_" }

Alphanumeral = _{ ASCII_ALPHANUMERIC | "_" }
