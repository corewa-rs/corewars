WHITESPACE = _{ " " | "\t" }

COMMENT = @{ ";" ~ (!NEWLINE ~ ANY)* }

Program = { SOI ~ (Line ~ NEWLINE+)+ ~ Line? ~ (EndProgram | EOI) }

EndProgram = { ^"END" ~ Expr? }

Line = _{ LabelDeclaration* ~ Instruction? }

Instruction = { Operation ~ Field ~ ("," ~ Field)? }

LabelDeclaration = _{ !(Operation | EndProgram) ~ Label ~ (":")? }

Label = @{ Alpha ~ Alphanumeral* }

Operation = ${ Opcode ~ ("." ~ Modifier)? }

Field = !{ AddressMode? ~ Expr }

Opcode = {
    ^"DAT" | ^"MOV" | ^"ADD" | ^"SUB" | ^"MUL" | ^"DIV" | ^"MOD" |
    ^"JMP" | ^"JMZ" | ^"JMN" | ^"DJN" | ^"CMP" | ^"SEQ" | ^"SNE" |
    ^"SLT" | ^"SPL" | ^"NOP" | ^"ORG" | ^"EQU"
}

Modifier = { ^"AB" | ^"BA" | ^"A" | ^"B" | ^"F" | ^"X" | ^"I" }

AddressMode = { "#" | "$" | "*" | "@" | "{" | "<" | "}" | ">" }

Expr = { Number | Label }

Number = @{ ("-" | "+")? ~ ASCII_DIGIT+ }

Alpha = _{ ASCII_ALPHA | "_" }

Alphanumeral = _{ ASCII_ALPHANUMERIC | "_" }
